---
date: 2021-07-22T12:52
---

# Deconstructing Lambdas

My notes on [Chris Penner](https://chrispenner.ca)'s talk, [Deconstructing
Lambdas](https://www.youtube.com/watch?v=xZmPuz9m2t0). All code is his! (With
some minor tweaks in some classes.)

## Functions Compose

```haskell
g . f
```

Run `f` on input, route output into input of `g`.

Encompassed by

```haskell
class Category k where
  id  :: k x x
  (.) :: k y z -> k x y -> k x z

(>>>) :: Category k => k x y -> k y z -> k x z
(>>>) = flip (.)
```

E.g.

```haskell
f :: a -> b
g :: b -> c
f >>> id >>> g
```

```{.graphviz}
digraph G {
  rankdir=LR
  a -> b [label="f"]
  b -> c [label="g"]
  b -> b [label="id"]
}
```

### Combinator Examples

```haskell
thrice :: Category k => k a a -> k a a
thrice k = k >>> k >>> k
```

Now, given

```haskell
instance Category (->) where
  id    = \x -> x
  g . f = \x -> g (f x)
```

We have

```haskell
add3 = thrice (+1)
-- >>> add3 10
-- 13
```

Let's try with another `Category`:

```haskell
newtype JSFunc a b = JSFunc { renderJS :: Text }

instance Category JSFunc where
  id    = JSFunc "(x => x)"
  g . f = JSFunc $ printf "(x => %s(%s(x)))" (renderJS g) (renderJS f)

times10 :: JSFunc Int Int
times10 = JSFunc "(x => x * 10)"

times1000 :: JSFunc Int Int
times1000 = thrice times10

-- >>> renderJS times1000
-- "(x => (x => x * 10)((x => (x => x * 10)((x => x * 10)(x)))(x)))"
```

## Using Arguments

In Haskell, functions can ignore and copy arguments at will, but this isn't the
case in other categories, e.g. linear functions, Rust, electrical circuits.

```haskell
class Category k => Cartesian k where
  -- In your category, how do you...
  copy    :: k a (a, a)  -- take one thing, turn it into two
  consume :: k a ()      -- take a thing and throw it away
  first   :: k (a, b) a
  second  :: k (a, b) b
  -- Are first and second derived from copy/ consume?
```

See [[da8e3558]].

```haskell
instance Cartesian (->) where
  copy    = \x      -> (x, x)
  consume = \_      -> ()
  first   = \(a, _) -> a
  second  = \(_, b) -> b

instance Cartesian JSFunc where
  copy    = JSFunc "(x        => [x, x])"
  consume = JSFunc "(_        => null)"
  first   = JSFunc "(([a, _]) => a)"
  second  = JSFunc "(([_, b]) => b)"
```

## Plumbing

How do we pass arguments to various expressions in the function definition? Or,
what's really going on here/ how do we write this without the ability to apply
functions to arguments:

```haskell
isPalindrome :: String -> Bool
isPalindrome s = reverse s == s

-- ==>

isPalindrome :: String -> Bool
isPalindrome =
  copy
  >>> (\(s, s') -> (reverse s, s'))  -- this right here; how to thread s through?
  >>> uncurry (==)
```

Encode with **strong** category:

```haskell
class Category k => Strong k where  -- maybe can be merged into Cartesian?
  apFst :: k a b -> k (a, x) (b, x)
  apSnd :: k a b -> k (x, a) (x, b)
```

E.g.

```haskell
f :: k a b
apFst f
```

```{.graphviz}
digraph {
  "(a, x)" -> "(b, x)" [label="f",headport=nw]
  "(a, x)" -> "(b, x)" [headport=ne]
}
```

Now we can write

```haskell
isPalindrome =
  copy
  >>> apFst reverse
  >>> uncurry (==)  -- still a bit of a handwave for now
```

Also:

```haskell
instance Strong (->) where
  apFst f = \(a, x) -> (f a, x)
  apSnd f = \(x, a) -> (x, f a)

instance Strong JSFunc where
  apFst f = JSFunc $ printf "(([a, x]) => [%s(a), x])" (renderJS f)
  apSnd f = JSFunc $ printf "(([x, a]) => [x, %s(a)])" (renderJS f)
```

### Combinator Examples

```haskell
(***) :: Strong k => k a a' -> k b b' -> k (a, b) (a', b')
f *** g = apFst f >>> apSnd g
```

E.g.

```haskell
f :: a -> a'
g :: b -> b'
f *** g
```

```{.graphviz}
digraph {
  "(a, b)" -> "(a', b)" [label="f",headport=nw]
  "(a, b)" -> "(a', b)" [headport=ne]
  "(a', b)" -> "(a', b')" [label="g",headport=ne]
  "(a', b)" -> "(a', b')" [headport=nw]
}
```

Or more succinctly:

```{.graphviz}
digraph {
  "(a, b)" -> "(a', b')" [label="f",headport=nw]
  "(a, b)" -> "(a', b')" [label="g",headport=ne]
}
```

Sort of an "apply both" or "parallel composition" (same idea as `(***)` from
[`Control.Arrow`][control.arrow]).

[control.arrow]: https://hackage.haskell.org/package/base-4.15.0.0/docs/Control-Arrow.html#v:-42--42--42-

```haskell
-- fanout
(&&&) :: Strong k => k x a -> k x b -> k x (a, b)
f &&& g = copy >>> (f *** g)
```

```{.graphviz}
digraph {
  x -> "(x, x)" [label="copy"]
  "(x, x)" -> "(a, b)" [label="f",headport=nw]
  "(x, x)" -> "(a, b)" [label="g",headport=ne]
}
```

## Primitives

Custom behavior unique to app or use case, not able to encode using totally
general classes (or just too much work).

```haskell
reverse' ::         k [a]    [a]
eq       :: Eq a => k (a, a) Bool

-- ==>

class MyPrim k where
  reverse' ::         k [a]    [a]
  eq       :: Eq a => k (a, a) Bool

instance MyPrim (->) where
  reverse' = reverse
  eq       = (==)

instance MyPrim JSFunc where
  reverse' = JSFunc "(s => s.split('').reverse().join(''))"
  eq       = JSFunc "(([a, b]) => a === b)"
```

Now we can write

```haskell
isPalindrome :: (Cartesian k, Strong k, MyPrim k) => k String Bool
isPalindrome =
  copy
  >>> apFst reverse'
  >>> eq

-- >>> isPalindrome "asdf"
-- False
-- >>> isPalindrome "racecar"
-- True
-- >>> renderJS isPalindrome
-- "(x => (x => (([a, b]) => a === b)((([a, x]) => [(s => s.split('').reverse().join(''))(a), x])(x)))((x => [x, x])(x)))"
```

## Functions Branch

UUT:

```haskell
collatzStep :: Int -> Int
collatzStep n =
  if even n
    then n `div` 2
    else (3 * n) + 1
```

Two bits of code (<code>n `div` 2</code>, `(3 * n) + 1`) that will never both
be run; select one branch to run. Useful to know whether something will branch.

Model with coproducts:

```haskell
class Category k => Cocartesian k where
  injectL :: k a            (Either a x)
  injectR :: k a            (Either x a)
  unify   :: k (Either a a) a
  -- | Non-essential but useful (2 * a = a + a)
  tag     :: k (Bool, a)    (Either a a)

instance Cocartesian (->) where
  injectL = \a -> Left a
  injectR = \a -> Right a
  unify   = \e ->
              case e of
                Left a -> a
                Right a -> a
  tag     = \(b, a) ->
              if b
                then Left a
                else Right a

instance Cocartesian JSFunc where
  injectL = JSFunc "(x => ({tag: 'left', value: x}))"
  injectR = JSFunc "(x => ({tag: 'right', value: x}))"
  unify   = JSFunc "(x => (x.value))"
  tag     = JSFunc "(([b, x]) => ({tag: b ? 'left' : 'right', value: x}))"
```

Running code on one branch or other (but may not need a separate class):

```haskell
class Category k => Choice k where
  left'  :: k a b -> k (Either a x) (Either b x)
  right' :: k a b -> k (Either x a) (Either x b)

instance Choice (->) where
  left' f  = \e ->
               case e of
                 Left a -> Left (f a)
                 Right x -> Right x
  right' f = \e ->
               case e of
                  Left x -> Left x
                  Right a -> Right (f a)

instance Choice JSFunc where
  left' (JSFunc f) = JSFunc $ printf "(x => x.tag === 'left' ? {tag: 'left', value: %s(x)} : x)" f
  right' (JSFunc f) = JSFunc $ printf "(x => x.tag === 'right' ? {tag: 'right', value: %s(x)} : x)" f
```

E.g. `left' f`:

```{.graphviz}
digraph {
  "Either a x" -> "Either b x" [headport=nw,label="f"]
  "Either a x" -> "Either b x" [headport=ne]
}
```

### Some Combinators

```haskell
(+++) :: Choice k => k a a' -> k b b' -> k (Either a b) (Either a' b')
f +++ g = left' f >>> right' g
```

(See [`(+++)`][arr choice] from `ArrowChoice`.)

[arr choice]: https://hackage.haskell.org/package/base-4.15.0.0/docs/Control-Arrow.html#v:-43--43--43-

And importantly, fanin, which is basically your `if` expression:

```haskell
(|||) :: (Cocartesian k, Choice k) => k a r -> k b r -> k (Either a b) r
f ||| g = f +++ g >>> unify
```

(See [`(|||)`][fanin] from `ArrowChoice`.)

[fanin]: https://hackage.haskell.org/package/base-4.15.0.0/docs/Control-Arrow.html#v:-124--124--124-

Back to UUT: must detect if input is even, then divide by two or do three times
input plus one.

First off, need some numbers. Here's one way: embedding Haskell `Int`s in the
category:

```haskell
class Numeric k where
  num     :: Int -> k a Int
  negate' :: k Int Int
  add     :: k (Int, Int) Int
  mult    :: k (Int, Int) Int
  div'    :: k (Int, Int) Int
  mod'    :: k (Int, Int) Int

instance Numeric (->) where
  num     = const
  negate' = negate
  add     = uncurry (+)
  mult    = uncurry (*)
  div'    = uncurry div
  mod'    = uncurry mod

instance Numeric JSFunc where
  num n   = JSFunc $ printf "(_ => (%d))" n
  negate' = JSFunc "(n => (-n))"
  add     = JSFunc "(([x, y]) => (x + y))"
  mult    = JSFunc "(([x, y]) => (x * y))"
  div'    = JSFunc "(([x, y]) => (x / y))"
  mod'    = JSFunc "(([x, y]) => (x % y))"
```

Helper:

```haskell
strong :: (Cartesian k, Strong k) => k (a, b) r -> k a b -> k a r
strong f x = copy >>> apSnd x >>> f

matchOn :: (Cartesian k, Strong k, Cocartesian k)
        => k a Bool -> k a (Either a a)
matchOn p = copy >>> apFst p >>> tag
```

Now for the parts of `collatzStep`:

```haskell
{-# LANGUAGE RankNTypes,ScopedTypeVariables #-}
isEven :: forall k. (Numeric k, Cartesian k, Strong k, Choice k, MyPrim k)
       => k Int Bool
isEven = mod2 >>> strong eq (num 0)   -- "n `mod` 2 == 0"
 where
  mod2 :: k Int Int
  mod2 = strong mod' (num 2)
```

And the function itself:

```haskell
-- TODO: figure out `NaN` in JS output
collatzStep :: (Cartesian k, Strong k, Cocartesian k, Numeric k, Choice k, MyPrim k)
            => k Int Int
collatzStep = matchOn isEven >>> onEven ||| onOdd
 where
  onEven = strong div' (num 2)
  onOdd  = strong mult (num 3) >>> strong add (num 1)
```

## New Interpretations

```haskell
newtype Diagram a b = Diagram { toGraph :: State Graph InputOutputLinks }
-- TODO: exercise: implement instances
```

## Functions can swap args around

```haskell
class Category k => Symmetric k where
  swap   :: k (a, b) (b, a)
  swapE  :: k (Either a b) (Either b a)
  assoc  :: k (a, (b, c)) ((a, b), c)
  assocE :: k (Either a (Either b c)) (Either (Either a b) c)
```

See <https://ncatlab.org/nlab/show/symmetric+monoidal+category>

## Functions can recurse

```haskell
class Cochoice k where
  unleft :: k (Either a c) (Either r c) -> k a r
```

The argument computation to `unleft` takes either an `a` or a `c` and produces
either a `r` (result) or a `c` (intermediate result). Specifically, the
computation `unleft f` must produce a `r`, but when `f` produces a `Right c`,
there is no `r` to return, so it must call `f` again (with the `c`) to see if
it can get a `r` this time.

E.g.

```haskell
f :: k (Either a c) (Either r c)
unleft f
```

```{.graphviz}
digraph {
  a -> "Either r c" [label="a"]
  "Either r c" -> "Either r c" [label="c"]
  "Either r c" -> r

  subgraph cluster_0 {
    label="f"; labeljust="l"
    "Either r c"
  }
}
```

Another loopy thing, fix-points:

```haskell
class Costrong k where
  unfirst :: k (a, c) (b, c) -> k a b
```

Now we can model:

- composition
- branching
- copying
- routing
- recursion
- fixpoints
- swapping

Can use these to encode classes for

- `Reader`
- `Writer`
- `State`
- `Cont`  :)
- `IO`

Since these monads have category equivalents.

## Functions as Data/ Free Functions

```haskell
{-# LANGUAGE GADTs #-}
data FreeFunc p a b where
  Id      :: FreeFunc p x x
  Compose :: FreeFunc p x y -> FreeFunc p y z -> FreeFunc p x z
  Copy    :: FreeFunc p x (x, x)
  Consume :: FreeFunc p x ()
  Fst     :: FreeFunc p (a, x) a
  Snd     :: FreeFunc p (x, a) a
  ApFst   :: FreeFunc p a b -> FreeFunc p (a, x) (b, x)
  ApSnd   :: FreeFunc p a b -> FreeFunc p (x, a) (x, b)
  -- etc.
```

Basically a data constructor for each of the methods we've seen so far.

```haskell
instance Category (FreeFunc k) where
  id  = Id
  (.) = flip Compose

instance Cartesian (FreeFunc k) where
  copy    = Copy
  consume = Consume
  first   = Fst
  second  = Snd

instance Strong (FreeFunc k) where
  apFst = ApFst
  apSnd = ApSnd

-- etc.
```

### Generalize over layer of primitives

```haskell
data FreeFunc p a b where
  -- ...
  Lift :: p a b -> FreeFunc p a b
```

E.g.

```haskell
data Prims a b where
  Reverse ::         Prims [a]    [a]
  Eq      :: Eq a => Prims (a, a) Bool

instance MyPrim (FreeFunc Prims) where
  reverse' = Lift Reverse
  eq       = Lift Eq
```

Now,

```haskell
{-# LANGUAGE QuantifiedConstraints,StandaloneDeriving #-}
deriving instance Show (Prims a b)
deriving instance (forall a b. Show (p a b)) => Show (FreeFunc p a b)

-- >>> show (isPalindrome @(FreeFunc Prims))
-- "Compose Copy (Compose (ApFst (Lift Reverse)) (Lift Eq))"
```

Not the most pretty or readable, but proves the concept of rendering/
transforming functions into visual representations. Also works for
serialization (e.g. to JSON [trickier to do from JSON since we need to infer
Haskell types from JSON values; would be easier w/ dependant types]).

With these approaches, we can now safely send code over the wire, since it can
be fully inspected on either end. For example, we can simply not provide a
primitive to delete files. We can check for totality/ termination by simply
seeing whether the `Costrong` or `Cochoice` are used anywhere. Can optimize the
categorical code (e.g. merging conditionals).

<!-- TODO: chris says `strong`/`costrong` could imply nontermination, rather
than `costrong`/`cochoice`; is this right? -->

## Parting ideas

- verilog
- redstone
- GPU
- SMT/SAT solvers
- probabilistic/ [bayesian](73705039.md) analysis (e.g. what are the odds of
    taking this branch?)
- non-determinism
- linear arrows
- FRP
- state machines
- free parallelization

Functions as data lets us analyze, optimize, serialize, etc. Syntax could use
work, but transformation from conventional Haskell to categorical style is
pretty mechanical so maybe the compiler could do it (a la `do`-notation).
