---
date: 2021-07-26T18:56
---

# Lenses

Lenses are [[997fa496]] which focus on a single item within a structure. A lens
must never fail to get or modify the item in focus. Because a lens can always
get a focus or set its value, it can also modify it using a function of the
current value (e.g. increment it) (commonly called `over`).

For example,

```haskell
{-# LANGUAGE TemplateHaskell #-}
import Control.Lens

data Foo = Foo { _x :: (Bool, String) } deriving Show
makeLenses ''Foo

>>> Foo {_x = (True, "hi")} ^. x . _2
"hi"
```

The expression `Foo {_x = (True, "hi")} ^. x . _2` was comprised of a few
parts:

```text
        Structure           Path
           |                 |
/----------+----------\    /-+--\
Foo {_x = (True, "hi")} ^. x . _2
                 \+-/   \/
                  |      |
                Focus  Action ("view")
```

The _path_ indicates a _focus_ within the _structure_. The _action_ does
something about whatever is being focused on. Notice that the parts of the path
are composed using regular function composition `(.)`!

An example using `over` (aka "modify"/ `%~`):

```haskell
>>> Foo {_x = (True, "hi")} & x . _1 %~ not
Foo {_x = (False,"hi")}
```

Lenses subsume the accessor pattern, bundling something akin to a getter and a
setter into a single value. For things like structs, we can even define
multiple lenses for the same field.

## Notes on types

Lenses are described (in the `lens` package and more broadly in convention) by
the type `Lens' s a` where `s` is the type of structure and `a` is the type of
focus, e.g.:

```haskell
_1 :: Lens' (a, b) a
-- s ~ (a, b)  i.e. _1 works on pair structures
-- a ~ a       i.e. _1 focuses on the first element of the pair
```

The lens actions have the following types:

```haskell
-- Simple versions (i.e. specialized to s ~ t and a ~ b):
view :: Lens' s a -> s        -> a
set  :: Lens' s a -> a        -> s -> s
over :: Lens' s a -> (a -> a) -> s -> s

-- Polymorphic versions:
view :: Lens s s a a -> s        -> a       -- the same, since it can't modify
set  :: Lens s t a b -> b        -> s -> t  -- often t involves b
over :: Lens s t a b -> (a -> b) -> s -> t
```

The infix/ operator variants are the same but with some arguments shuffled
around.

I think the name `over` makes more sense in the long-hand form:

```haskell
>>> let f = Foo{_x = (True, "hello")}
>>> over (x . _2) (`mappend` " there") f
Foo{_x=(True, "hello there")}
```

## Creating lenses

The template Haskell tool `makeLenses` used above to create `x :: Lens' Foo
(Bool, String)` could have been replaced by a manual definition using the
`lens` helper:

```haskell
lens :: (s -> a) -> (s -> a -> s) -> Lens' s a

x :: Lens' Foo (Bool, String)
x = lens _x (\foo x' -> foo{_x = x'})
```

Note

```haskell
_x :: Foo -> (Bool, String)  -- "getter"; from definition of Foo as record type
\foo x' -> foo{_x = x'} :: Foo -> (Bool, String) -> Foo -- "setter"
```

## Interesting combinators/ actions

```haskell
>>> Right "right" ^. chosen
"right"
>>> Left "left" ^. chosen
"left"

-- Using simplified types:
(+~), (-~), (*~) :: Num a => Lens' s a -> a -> s -> s
>>> (True, 10) & _2 *~ 3
(True,30)
(//~) :: Fractional a => Lens' s a -> a -> s -> s

-- "update by raising to power"; not related to viewing:
(^~)  :: (Integral e, Num a)        => Lens' s a -> e -> s -> s
(^^~) :: (Integral e, Fractional a) => Lens' s a -> e -> s -> s
(**~) :: Floating a                 => Lens' s a -> e -> s -> s

(||~), (&&~) :: Lens s Bool -> Bool -> s -> s

(<>~) :: Semigroup m => Lens s m -> m -> s -> s
>>> ("foo", True) & _1 <>~ "bar"
("foobar",True)
```

### Operator conventions

There are a few conventions for deciphering the meaning of a lens operator
without looking it up:

- **^** Often means viewing/ getting something
- **.** Is sort of the end of the sentence ("`^.`" = _get_ the focus without
    doing anything else)
- **%** Means **mod**ify using a function
- **~** Means update/ set

And some "adverb"-like symbols (used as prefixes to setting/ updating actions):

- **<** In addition to modifying focus, return the new value `("a", 1) & _2 <+~
    5 == (6, ("a", 6))`
- **<<** In addition to modifying the focus, return the old value `("a", 1) &
    _2 <<+~ 5 == (1, ("a", 6))`

## Lens laws

The lens laws (like typeclass laws for polymorphic code) help reason about
lens-y code and can indicate why odd-feeling or poorly behaved lenses feel or
behave the way they do. Note:

> Typically you should follow these laws when writing field accessors and basic
> data accessors, but there’s a bit more wiggle room if you’re using lenses to
> emulate virtual fields or to do input cleanup/correction.
>
> _via Penner in_ Optics by Example

```haskell
-- 1. set then get
\lens val s -> view lens (set lens val s) === val

-- 2. get then set (set with get)
\lens s -> set lens (view lens s) s === s

-- 3. set then set
\lens val1 val2 s -> set lens val2 (set lens val1 s) === set lens val2 s
```

You can break the laws if you really need to, but that often indicates you
should be reaching for a different kind of optic.

Lenses which break the laws seem like they base results off of existing data
when they shouldn't, e.g.:

```haskell
foobar :: Lens' Foo (Bool, String)
foobar = lens _x (\f (_, s) -> Foo (not $ f ^. x._1, s))
```

In this case, the setter is a function of the existing `Bool` value and ignores
the new value given by the caller of the setter.

```haskell
>>> let (val1, val2) = ((True, "val1"), (True, "val2"))
-- 1. set then get
>>> view foobar (set foobar val1 f) == val1
False
-- 2. get then set
>>> set foobar (view foobar f) f == f
False
-- 3. set then set
>>> set foobar (True, "val2") (set foobar (True, "val1") f) == set foobar val2 f
False
```

## Resources

- [_History of Lenses_, Ed Kmett][history]

[history]: https://github.com/ekmett/lens/wiki/History-of-Lenses
