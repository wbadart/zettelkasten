---
date: 2020-04-07T13:17
---

# Pattern Synonyms

Pattern synonyms are aliases for pattern matching expressions in
Haskell (enabled by the `PatternSynonyms` GHC extension).

Pattern synonyms allow you to introduce abstractions over complicated
data constructions. FPComplete gives the following [example][01]
around a `Date` data type:

```haskell
data Date = Date { month :: Int, day :: Int } deriving Show
```

Functions which consume dates might be interested in having different
formulas for different dates:

```haskell
greet :: Date -> String
greet (Date 1 1)   = "Happy new year!"
greet (Date 12 25) = "Merry Christmas!"
greet _            = "hi"
```

Pattern synonyms let us name these patterns:

```haskell
{-# LANGUAGE PatternSynonyms #-}
pattern January day  = Date { month = 1, day = day }
pattern February day = Date { month = 2, day = day}
-- ...
pattern NewYears  = January 1
pattern Christmas = December 25

greet' :: Date -> String
greet' NewYears  = "Happy new year!"
greet' Christmas = "Merry Christmas!"
greet' _         = "hi"
```

## Bidirectional vs. Unidirectional Pattern Synonyms

The above patterns, declared with `=`, are bidirectional pattern
synonyms. These can be used for pattern matching as above _and_ to
construct values, e.g.:

```haskell
myFavoriteDay :: Date
myFavoriteDay = Christmas
```

Unidirectional pattern synonyms, declared with `<-` do not support
this. It seems they should be used when it is inappropriate or
impossible to construct a value from the pattern. FPComplete's
example for the "impossible" case is pattern matching on a run-time
condition:

```haskell
pattern BeforeChristmas <- December (compare 25 -> GT)
pattern Christmas       <- December (compare 25 -> EQ)
pattern AfterChristmas  <- December (compare 25 -> LT)

react :: Date -> String
react BeforeChristmas = "Waiting :("
react Christmas       = "Presents!"
react AfterChristmas  = "Have to wait a whole year :("
react _               = "It's not even December..."
```

> There are several things to note
> - We used a bidirectional pattern (`December`) to define the new
>   patterns
> - These uni-directional patterns cannot be used as expressions (it is
>   not obviously what the values of `BeforeChristmas` and `AfterChristmas`
>   ought to be anyway)
> - We used [view patterns](df956891.md) to compare
>   the given day to 25 in `(compare 25 >   -> ...)`

Recall that Matt Noonan uses pattern synonyms in [Ghosts of Departed
Proofs](TODO) as a way to pattern match against data with evidence
without giving users a way to construct arbitrary evidence.

As with view patterns, I was introduced by [_24 Days of GHC Extensions_][02].

[01]: https://web.archive.org/web/20150924122329/https://www.fpcomplete.com/user/icelandj/Pattern%20synonyms
[02]: https://ocharles.org.uk/posts/2014-12-03-pattern-synonyms.html
