---
date: 2020-10-19T11:27
tags:
- fp
- algebra
---

# Catamorphism

A [recursion scheme](ded70ad5.md) that tears down a recursive structure into a
value. From the Greek "[κατά][wiktionary]" for "downwards" (e.g. "catastrophe")
and "μορφή" for "shape".

[wiktionary]: https://en.wiktionary.org/wiki/%CE%BA%CE%B1%CF%84%CE%AC

```haskell
cata :: (Functor f) => FAlgebra f a -> Fix f -> a
cata f = out >>> fmap (cata f) >>> f
```

where

```haskell
type FAlgebra f a = f a -> a
newtype Fix f = In { out :: f (Fix f) }
```

For example, given:

```haskell
{-# LANGUAGE DeriveFunctor #-}
data StringOrIntList f
  = StringElement String f
  | IntElement    Int    f
  | Nil
  deriving Functor

xs :: Fix StringOrIntList
xs = In (StringElement "hello"
     (In (StringElement "world"
     (In (IntElement 10
     (In Nil))))))
```

we can write:

```haskell
concatElems :: FAlgebra StringOrIntList String
concatElems x = case x of
  StringElement s other -> s      <> other
  IntElement    i other -> show i <> other
  Nil                   -> ""

main = putStrLn (cata concatElems xs)  -- "helloworld10"
```

It seems that if catamorphism is the only recursion scheme you'll be using on
your recursive data structure du jour, then you're probably best off sticking
to `foldr`. But if you do need (or anticipate needing) the more advanced folds,
catamorphisms are the way to go. This will let you write your simple folds in
the "same language" as your advanced folds (and unfolds).

[Tim Williams] uses F-algebras that return an instance of the fixed functor for
certain mapping tasks, like variable substitution and (composable) optimization
pipelines in an AST.

[Tim Williams]: https://www.youtube.com/watch?v=Zw9KeP3OzpU
